apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.0'

    defaultConfig {
        applicationId "com.fionera.rxgank"
        minSdkVersion 25
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res/layouts/test',
                    'src/main/res/layouts/recycler_item',
            ]
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    debugImplementation project(path: ':base')
    releaseImplementation project(path: ':base')

    implementation 'com.android.support:appcompat-v7:26.0.0-beta2'
    implementation 'com.android.support:appcompat-v7:26.0.0-beta2'
    implementation 'com.android.support:recyclerview-v7:26.0.0-beta2'
    implementation 'com.android.support:support-v4:26.0.0-beta2'
    implementation 'com.android.support:design:26.0.0-beta2'
    implementation 'com.android.support:transition:26.0.0-beta2'
    implementation 'com.android.support:customtabs:26.0.0-beta2'
    implementation 'com.android.support:support-dynamic-animation:26.0.0-beta2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Dagger2
    implementation 'com.google.dagger:dagger-android:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'

    // Stetho-OkHttp-Realm
    debugImplementation 'com.facebook.stetho:stetho:1.4.2'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.4.2'
    debugImplementation 'com.uphyca:stetho_realm:2.0.0'
}

class BuildTimeListener implements TaskExecutionListener, BuildListener {
    private org.gradle.internal.time.Clock clock
    private times = []

    @Override
    void beforeExecute(Task task) {
        clock = new org.gradle.internal.time.Clock()
    }

    @Override
    void afterExecute(Task task, TaskState taskState) {
        def ms = clock.getElapsedMillis()
        times.add([ms, task.path])

//        task.project.logger.warn "${task.path} spend ${ms}ms"
    }

    @Override
    void buildStarted(Gradle gradle) {

    }

    @Override
    void settingsEvaluated(Settings settings) {

    }

    @Override
    void projectsLoaded(Gradle gradle) {

    }

    @Override
    void projectsEvaluated(Gradle gradle) {

    }

    @Override
    void buildFinished(BuildResult result) {
        println "Task spend time:"
        for (time in times) {
            if (time[0] >= 50) {
                printf "%7sms  %s\n", time
            }
        }
    }

}

project.gradle.addListener(new BuildTimeListener())
